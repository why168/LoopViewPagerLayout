plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

group = 'com.github.why168'
version = versions.versionName

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        consumerProguardFiles 'proguard-rules.txt'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        versionCode versions.versionCode
        versionName versions.versionName
    }

    compileOptions {
        sourceCompatibility versions.sourceCompatibility
        targetCompatibility versions.targetCompatibility
    }

    lintOptions {
        textOutput 'stdout'
        textReport true
        lintConfig rootProject.file('lint.xml')
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    buildTypes {
        release {
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api deps.androidxLifecycle
    implementation deps.androidxAnnotations
    implementation deps.androidxCore
    compileOnly 'androidx.appcompat:appcompat:1.5.1'


    testImplementation deps.junit

    androidTestImplementation deps.androidxJunit
    androidTestImplementation deps.androidxTestRunner
}
//---------------------------------------------

// 指定编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// 打包源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.api
    classpath += configurations.implementation
    classpath += configurations.compileOnly
}

// 制作文档(Javadoc)
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.why168'
                artifactId = 'LoopViewPagerLayout'
                version = versions.versionName
            }
        }
    }
}
